
Процедура ОбработкаПроведения(Отказ, Режим)

	Если ТипОперации = Перечисления.ТипыОпераций.Приход Тогда
	
		ДобавитьДвижениеДенег(ВидДвиженияНакопления.Приход);
	
	ИначеЕсли ТипОперации = Перечисления.ТипыОпераций.Расход Тогда
	
		ДобавитьДвижениеДенег(ВидДвиженияНакопления.Расход);
	
	ИначеЕсли ТипОперации = Перечисления.ТипыОпераций.Перевод Тогда
	
		ОбработатьПеремещениеДенег();
	
	ИначеЕсли ТипОперации = Перечисления.ТипыОпераций.ОбменВалюты Тогда
	
		ОбработатьПеремещениеДенег();
	
	ИначеЕсли ТипОперации = Перечисления.ТипыОпераций.МыВзялиВДолг Тогда
	
		ОбработатьВзаиморасчеты(ВидДвиженияНакопления.Приход);
		ДобавитьДвижениеДенег(ВидДвиженияНакопления.Приход);
	
	ИначеЕсли ТипОперации = Перечисления.ТипыОпераций.МыВернулиДолг Тогда
	
		ОбработатьВзаиморасчеты(ВидДвиженияНакопления.Расход);
		ДобавитьДвижениеДенег(ВидДвиженияНакопления.Расход);
	
	ИначеЕсли ТипОперации = Перечисления.ТипыОпераций.МыДалиВДолг Тогда
	
		ОбработатьВзаиморасчеты(ВидДвиженияНакопления.Расход);
		ДобавитьДвижениеДенег(ВидДвиженияНакопления.Расход);
	
	ИначеЕсли ТипОперации = Перечисления.ТипыОпераций.НамВернулиДолг Тогда
	
		ОбработатьВзаиморасчеты(ВидДвиженияНакопления.Приход);
		ДобавитьДвижениеДенег(ВидДвиженияНакопления.Приход);
	
	ИначеЕсли ТипОперации = Перечисления.ТипыОпераций.ВводОстатка Тогда
	
		Сообщить("Пока не реализовано!");	
	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработатьПеремещениеДенег()
	
	Движения.ДвижениеДенегПоСчетам.Записывать = Истина;
	Для Каждого ТекСтрокаДанныеОперации Из ДанныеОперации Цикл
		// Вычитаем сумму с первого счета...
		Движение = Движения.ДвижениеДенегПоСчетам.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Счет = ТекСтрокаДанныеОперации.Счет;
		Движение.Контакт = ТекСтрокаДанныеОперации.Контакт;
		Движение.Статья = ТекСтрокаДанныеОперации.Статья;
		Движение.Сумма = ТекСтрокаДанныеОперации.Сумма;
		// ... в пользу второго
		Движение = Движения.ДвижениеДенегПоСчетам.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.Счет = ТекСтрокаДанныеОперации.СчетПеремещения;
		Движение.Контакт = ТекСтрокаДанныеОперации.Контакт;
		Движение.Статья = ТекСтрокаДанныеОперации.Статья;
		Движение.Сумма = ТекСтрокаДанныеОперации.Сумма;
	КонецЦикла;

КонецПроцедуры

Процедура ОбработатьВзаиморасчеты(ВидДвиж)
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Для Каждого ТекСтрокаДанныеОперации Из ДанныеОперации Цикл
		Движение = Движения.Взаиморасчеты.Добавить();
		Движение.ВидДвижения = ВидДвиж;
		Движение.Период = Дата;
		Движение.Контакт = ТекСтрокаДанныеОперации.Контакт;
		Движение.Статья = ТекСтрокаДанныеОперации.Статья;
		Движение.Сумма = ТекСтрокаДанныеОперации.Сумма;
		Движение.Счет = ТекСтрокаДанныеОперации.Счет;
	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьДвижениеДенег(ВидДвиж)
		
	Движения.ДвижениеДенегПоСчетам.Записывать = Истина;
	Для Каждого ТекСтрокаДанныеОперации Из ДанныеОперации Цикл
		Движение = Движения.ДвижениеДенегПоСчетам.Добавить();
		Движение.ВидДвижения = ВидДвиж;
		Движение.Период = Дата;
		Движение.Счет = ТекСтрокаДанныеОперации.Счет;
		Движение.Контакт = ТекСтрокаДанныеОперации.Контакт;
		Движение.Статья = ТекСтрокаДанныеОперации.Статья;
		Движение.Сумма = ТекСтрокаДанныеОперации.Сумма;
	КонецЦикла;
	
КонецПроцедуры

